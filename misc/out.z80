; Misc Output Functions.


; Output a single byte in binary on the display.
; Params:
;   e - The byte to display.
; Destroys:
;   b, a.
Out_Bin_Byte:
  ld b, 8 ; Get ready for 8 iterations.
_Out_Bin_Byte_Loop:
  rl e    ; Rotate leading bit of e into carry.

  ld a, $30 ; Load '0' into a.
  jr nc, _Out_Bin_Byte_Loop_End
  inc a     ; If carry flag is set, increment a.
_Out_Bin_Byte_Loop_End:
  call _PutC
  djnz _Out_Bin_Byte_Loop
  
  rl e  ; Restore e.
  ret 


; Output a single byte in binary as a string in memory.
; Params:
;   e  - The byte to convert to a string.
;   hl - The address in memory to place the string. 
; Returns:
;   The null terminated binary string found in e located at hl.
; Destroys:
;   b, a, hl
SOut_Bin_Byte:
  ld b, 8 ; Get ready for iterations.
_SOut_Bin_Byte_Loop:
  rl e

  ld a, $30   ; Load '0' into a. 
  jr nc, _SOut_Bin_Byte_Loop_End
  inc a       ; Switch a to '1'
_SOut_Bin_Byte_Loop_End:
  ld (hl), a  ; Store character in memory.
  inc hl      ; Advance pointer in memory.
  djnz _SOut_Bin_Byte_Loop

  ld (hl), $00  ; Load null character into memory.
  rl e
  ret 

